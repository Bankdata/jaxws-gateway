
group 'dk.bankdata.jaxws'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    toolVersion = '8.12'
}

test {
    jacoco {
        includes = ['*.*']
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly(group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxws', version: project['org.apache.cxf.version'])
    compileOnly(group: 'org.apache.cxf', name: 'cxf-rt-transports-http', version: project['org.apache.cxf.version'])
    compile(group: 'org.slf4j', name: 'slf4j-api', version: project['org.slf4j.version'])

    compileOnly(group: 'io.opentracing', name: 'opentracing-api', version: project['io.opentracing.version'])
    compileOnly(group: 'io.opentracing', name: 'opentracing-util', version: project['io.opentracing.version'])

    compileOnly(group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: project ['javax.ws.rs.api.version'])
    compileOnly(group: 'javax.enterprise', name: 'cdi-api', version: project['javax.enterprise.version'])

    compileOnly(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project['com.fasterxml.jackson.version'])
    compileOnly(group: 'javax.validation', name: 'validation-api', version: project['javax.validation.version'])
    compileOnly(group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: project['com.fasterxml.jackson.version'])

    testCompile(group: 'javax.enterprise', name: 'cdi-api', version: project['javax.enterprise.version'])
    testCompile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project['com.fasterxml.jackson.version'])
    testCompile(group: 'junit', name: 'junit', version: project['junit.version'])
    testCompile(group: 'org.mockito', name: 'mockito-core', version: project['org.mockito.version'])
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'dk.bankdata.jaxws'
            artifactId 'gateway'
            version version

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.hasProperty('ossrhUsername') ? ossrhUsername : ''
                password project.hasProperty('ossrhPassword') ? ossrhPassword : ''
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'JaxWS-Gateway'
            description 'JaxWs-Gateway helps work with SOAP calls'
            url 'https://github.com/Bankdata/jaxws-gateway'
            scm {
                connection 'scm:git:https://github.com/Bankdata/jaxws-gateway.git'
                developerConnection 'scm:git:https://github.com/Bankdata/jaxws-gateway.git'
                url 'https://github.com/Bankdata/jaxws-gateway'
                tag 'HEAD'
            }
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id 'bogedal'
                    name 'Kenneth BÃ¸gedal'
                    email 'kbh@bankdata.dk'
                    organization 'Bankdata'
                    organizationUrl 'https://www.bankdata.dk'
                }
            }
        }
    }
}
